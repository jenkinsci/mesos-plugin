plugins {
    id 'idea'
    id 'java-library'
    id "com.diffplug.gradle.spotless" version "3.19.0"
    id "org.jenkins-ci.jpi" version "0.30.0-SNAPSHOT"
}

repositories {
    jcenter()
}

ext {
    usiBranch = 'master'
}

dependencies {
    // This dependency is exported to consumers, that is to say found on their compile classpath.
    api 'org.apache.commons:commons-math3:3.6.1'

    // This dependency is used internally, and not exposed to consumers on their own compile classpath.
    implementation 'com.google.guava:guava:26.0-jre'
    implementation('com.mesosphere.usi:core') { version { branch = usiBranch } }
    implementation('com.mesosphere.usi:core-models') { version { branch = usiBranch } }
    implementation('com.mesosphere.usi:mesos-client') { version { branch = usiBranch } }
    implementation group: 'org.slf4j', name: 'slf4j-api', version: '1.7.26'

    // Test dependencies
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.3.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.3.1'
    testImplementation('com.mesosphere.usi:test-utils') { version { branch = usiBranch } }
}

test {
    useJUnitPlatform()
}

group = "org.jenkins-ci.plugins"
description = "Allows the dynamic launch Jenkins agent on a Mesos cluster, depending on workload"
sourceCompatibility = 1.11
targetCompatibility = 1.11

jenkinsPlugin {
    coreVersion = "2.155"
    displayName = "Mesos Cloud"
    url = "https://wiki.jenkins-ci.org/display/JENKINS/Mesos+Plugin"
    gitHubUrl = "https://github.com/mesosphere/mesos-plugin/"
    shortName = "mesos"
 
    // The developers section is optional, and corresponds to the POM developers section.
    developers {
    }
}

spotless {
    java {
        googleJavaFormat()
    }
}

idea {
    project {
        jdkName = '1.11'
        languageLevel = '1.11'
        vcs = 'Git'
    }
}
