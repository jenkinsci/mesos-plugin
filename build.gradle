plugins {
    id 'idea'
    id 'java-library'
    id "com.diffplug.gradle.spotless" version "3.19.0"
    id "com.github.blueboxware.tocme" version "1.1"
    id "com.github.spotbugs" version "1.7.1"
    id "org.jenkins-ci.jpi" version "0.31.0"
}

repositories {
    jcenter()
    maven {
        name "Mesosphere Public Repo"
        url "https://downloads.mesosphere.com/maven"
    }
}

ext {
    usiVersion= '0.1.9'
    scalaVersion = '2.12'
    akkaVersion = '2.5.19'
}

dependencies {
    // This dependency is exported to consumers, that is to say found on their compile classpath.
    api 'org.apache.commons:commons-math3:3.6.1'
    api 'com.google.guava:guava:26.0-jre'
    api group: 'com.mesosphere.usi', name: 'core', version: usiVersion
    api group: 'com.mesosphere.usi', name: 'core-models', version: usiVersion
    api group: 'com.mesosphere.usi', name: 'mesos-client', version: usiVersion
    api group: 'com.mesosphere.usi', name: 'persistence', version: usiVersion
    api group: 'org.slf4j', name: 'slf4j-api', version: '1.7.25'

    // Test dependencies
    testImplementation 'com.squareup.okhttp3:okhttp:3.14.1'
    testImplementation 'javax.json:javax.json-api:1.1'
    testImplementation 'org.glassfish:javax.json:1.0.4'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.3.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.3.1'
    testImplementation group: 'com.mesosphere.usi', name: 'test-utils', version: usiVersion
    testImplementation 'org.awaitility:awaitility:3.1.6'

    compileOnly 'com.github.spotbugs:spotbugs-annotations:3.1.3'
}

test {
    useJUnitPlatform()
}

group = "org.jenkins-ci.plugins"
description = "Allows the dynamic launch Jenkins agent on a Mesos cluster, depending on workload"
sourceCompatibility = 1.8
targetCompatibility = 1.8

jenkinsPlugin {
    coreVersion = "2.164"
    displayName = "Mesos Cloud"
    url = "https://wiki.jenkins-ci.org/display/JENKINS/Mesos+Plugin"
    gitHubUrl = "https://github.com/mesosphere/jenkins-mesos-plugin/"
    shortName = "mesos"

    // The developers section is optional, and corresponds to the POM developers section.
    developers {
        developer {
            id 'jeschkies'
            name 'Karsten Jeschkies'
            email 'kjeschkies@mesosphere.com'
        }
    }
}

tasks.withType(com.github.spotbugs.SpotBugsTask) {
    reports {
        xml.enabled = false
        html.enabled = true
    }
}

spotless {
    java {
        googleJavaFormat()
    }
}

idea {
    project {
        jdkName = '1.8'
        languageLevel = '1.8'
        vcs = 'Git'
    }
}

tocme {
    variant = GitHub
    doc(file("README.md"))
}
